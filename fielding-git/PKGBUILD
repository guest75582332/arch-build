# Maintainer: Leandro Guedes <leanguedes at icloud dot com>

pkgname='fielding-git'
pkgver='r289.a964edf'
pkgrel='1'
pkgdesc='A simple REST API testing tool'
arch=(x86_64)
url="https://apps.kde.org/${pkgname%-git}/"
license=('GPL-3.0-or-later')
depends=('kconfig'
         'kcoreaddons'
         'kdbusaddons'
         'ki18n'
         'kirigami'
         'kirigami-addons'
         'qqc2-desktop-style'
         'qt6-base'
         'qt6-declarative'
         'qt6-svg')
makedepends=('extra-cmake-modules'
             'git'
             'vulkan-headers')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://invent.kde.org/utilities/${pkgname%-git}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${pkgname%-git} -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
