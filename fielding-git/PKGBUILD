# Maintainer: Leandro Guedes <leanguedes at icloud dot com>

_pkgname='fielding'
pkgname="${_pkgname}-git"
pkgver='r289.a964edf'
pkgrel='1'
pkgdesc='A simple REST API testing tool'
arch=('x86_64')
url="https://apps.kde.org/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('gcc-libs'
         'glibc'
         'hicolor-icon-theme'
         'kconfig'
         'kcoreaddons'
         'kdbusaddons'
         'ki18n'
         'kirigami'
         'kirigami-addons'
         'qqc2-desktop-style'
         'qt6-base'
         'qt6-declarative'
         'qt6-svg')
makedepends=('extra-cmake-modules'
             'git'
             'vulkan-headers')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://invent.kde.org/utilities/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S "${_pkgname}" -DBUILD_TESTING='OFF'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
