# Maintainer: Leandro Guedes <leanguedes at icloud dot com>

pkgname='fielding-git'
pkgver='r231.6483fa1'
pkgrel='1'
pkgdesc='A simple REST API testing tool'
arch=(x86_64)
url='https://apps.kde.org/keepsecret/'
license=('GPL-3.0-or-later')
depends=('gcc-libs'
         'glib2'
         'glibc'
         'kconfig'
         'kcoreaddons'
         'kdbusaddons'
         'ki18n'
         'kirigami'
         'kirigami-addons'
         'kitemmodels'
         'qqc2-desktop-style'
         'qt6-base'
         'qt6-declarative'
         'qt6-svg')
makedepends=('extra-cmake-modules'
             'git'
             'kdoctools'
             'vulkan-headers')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://invent.kde.org/utilities/${pkgname%-git}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${pkgname%-git} -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
