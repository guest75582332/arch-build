# Maintainer: Leandro Guedes <leanguedes at icloud dot com>

_pkgname='hash-o-matic'
pkgname='hashomatic'
pkgver='1.0.1'
pkgrel='1'
pkgdesc='Simple hash validator'
arch=('x86_64')
url="https://apps.kde.org/$pkgname"
license=('LGPL-2.1-or-later')
depends=('gcc-libs'
         'glibc'
         'hicolor-icon-theme'
         'kconfig'
         'kcoreaddons'
         'kdbusaddons'
         'ki18n'
         'kirigami'
         'kirigami-addons'
         'qqc2-desktop-style'
         'qt6-base'
         'qt6-declarative'
         'qt6-svg')
makedepends=('extra-cmake-modules'
             'vulkan-headers')
source=("https://download.kde.org/stable/${_pkgname}/${_pkgname}-$pkgver.tar.xz")
sha256sums=('bfabdc12234f9ae73ba652d881d66e9220fba3f8bbf6074fcde6a0e90cc7fc1a')

build() {
  cmake -B build -S ${_pkgname}-$pkgver -DBUILD_TESTING='OFF'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
